module calc_stat_mod
   use fst_mod, only: fst_open,fst_find,fst_read,fst_close
   use base_stats_mod, only: tTest
   implicit none
   private
   public :: calc_filename,calc_read,calc_t1, calc_r, calc_nt

#include <msg.h>
#include <arch_specific.hf>
#include <rmnlib_basics.hf>
#include <clib_interface.cdk>

   integer,save,public :: calc_nmembers = 0

contains

   !/*
      function calc_filename(F_filename_S,F_class_S,F_varname_S,F_level,F_hour) result(F_istat)
      character(len=*),intent(out) :: F_filename_S
      character(len=*),intent(in) :: F_class_S
      character(len=*),intent(in) :: F_varname_S
      integer,intent(in) :: F_level,F_hour
      integer :: F_istat
      !*/
      character(len=4) :: varname_S
      character(len=128) :: prefix_S,class_S
      integer :: istat
      !----------------------------------------------------------------------
      F_istat = RMN_ERR
      class_S = F_class_S
      istat = clib_tolower(class_S)
      select case (class_S)
      case ('pre')
         prefix_S = 'sverif-precalc_'
      case ('tstat')
         prefix_S = 'sverif-tstats_'
      case ('aux')
         prefix_S = 'sverif-aux_'
      case DEFAULT
         call msg(MSG_ERROR,'(calc_stat_mod) Unknown class '//trim(F_class_S)//' ... use "pre", "tstat" or "aux"')
         return
      end select
      varname_S = F_varname_S
      istat = clib_tolower(varname_S)
      write(F_filename_S,'(a,I4.4,a,I4.4,a)') trim(prefix_S)//trim(varname_S),F_level,'_',F_hour,'h.dat'
      F_istat = RMN_OK
      !----------------------------------------------------------------------
   end function calc_filename


   !/*
   function calc_read(F_data,F_filename_S,F_varname_S,F_level,F_hour) result(F_istat)
      implicit none
      real, pointer :: F_data(:,:,:)
      character(len=*), intent(in) :: F_filename_S,F_varname_S
      integer, intent(in) :: F_level,F_hour
      integer :: F_istat
      !*/
      logical :: READ_ONLY_L = .true.
      integer :: fileid,istat,key,datev
      !----------------------------------------------------------------------
      F_istat = RMN_ERR
      fileid = fst_open(F_filename_S,READ_ONLY_L)
      if (.not.RMN_IS_OK(fileid)) then
         call msg(MSG_ERROR,'(calc_stat_mod) Problem opening member file '//trim(F_filename_S))
         return
      endif
      datev = RMN_ANY_DATE
      key = fst_find(fileid,F_varname_S,datev,F_level,F_hour,RMN_ANY_I)
      if (.not.RMN_IS_OK(key)) then
         call msg(MSG_ERROR,'(calc_stat_mod) Problem finding member file field: '//trim(F_varname_S)//' in '//trim(F_filename_S))
         return
      endif
      nullify(F_data)
      F_istat = fst_read(key,F_data,key)
      if (.not.RMN_IS_OK(F_istat)) then
         call msg(MSG_ERROR,'(calc_stat_mod) Problem reading member file field: '//trim(F_varname_S)//' in '//trim(F_filename_S))
         return
      endif
      istat = fst_close(fileid)
      call msg(MSG_INFO,'(calc_stat_mod)Reading member file field: '//trim(F_varname_S)//' in '//trim(F_filename_S))
      !----------------------------------------------------------------------
      return
   end function calc_read

   !/*
   function calc_t1(F_data,F_exavg_8,F_gss0_8,F_gss_8,F_tstat) result(F_istat)
      implicit none
      real, dimension(:,:), intent(in) :: F_data
      real(RDOUBLE), intent(in) :: F_exavg_8,F_gss0_8,F_gss_8
      real(RDOUBLE), intent(out) :: F_tstat
      integer :: F_istat
      !*/
      integer :: ii,jj
      real(RDOUBLE) :: exvar_8,rni,rnj,rnt
      !----------------------------------------------------------------------
      if (calc_nmembers < 3) then
         F_istat = RMN_ERR
         call msg(MSG_ERROR,'(calc_stat_mod) Numbers of members should be >= 3')
         return
      endif
      F_istat = RMN_OK
      rni = dble(size(F_data,dim=1))
      rnj = dble(size(F_data,dim=2))
      rnt = dble(calc_nmembers)
      exvar_8 = (F_gss_8+F_gss0_8)/(rni*rnj*rnt*(rni*rnj*rnt-1d0))
      F_tstat = (F_exavg_8 - sum(dble(F_data))/size(F_data))/sqrt(exvar_8)
      !----------------------------------------------------------------------
   end function calc_t1

   !/*
   function calc_r(F_data,F_exavg_8,F_eavg_8,F_tstat) result(F_istat)
      implicit none
      real, dimension(:,:), intent(in) :: F_data
      real(RDOUBLE), intent(in) :: F_exavg_8
      real(RDOUBLE), dimension(:,:), intent(in) :: F_eavg_8
      real(RDOUBLE), intent(out) :: F_tstat
      integer :: F_istat
      !*/
      integer :: ii,jj
      real(RDOUBLE) :: avg_8,exss_8,excor_8
      !----------------------------------------------------------------------
      if (calc_nmembers < 3) then
         F_istat = RMN_ERR
         call msg(MSG_ERROR,'(calc_stat_mod) Numbers of members should be >= 3')
         return
      endif
      F_istat = RMN_OK
      avg_8 = (sum(dble(F_data))/size(F_data))
      excor_8 = 0.
      exss_8 = 0.
      do jj=1,size(F_data,dim=2)
         do ii=1,size(F_data,dim=1)
            excor_8 = excor_8 + &
                 (F_eavg_8(ii,jj)-F_exavg_8)*(dble(F_data(ii,jj))-avg_8)
            exss_8 = exss_8 + &
                 sqrt((F_eavg_8(ii,jj)-F_exavg_8)**2 * (dble(F_data(ii,jj))-avg_8)**2)
         enddo
      enddo
      F_tstat = log(1.d0 - min(excor_8/exss_8,1d0-epsilon(exss_8)))
      !----------------------------------------------------------------------
   end function calc_r

   !/*
   function calc_nt(F_crit,F_data,F_eavg_8,F_evar0_8,F_evar_8,F_tstat) result(F_istat)
      implicit none
      real, intent(in) :: F_crit
      real, dimension(:,:), intent(in) :: F_data
      real(RDOUBLE), dimension(:,:), intent(in) :: F_eavg_8,F_evar0_8,F_evar_8
      real(RDOUBLE), intent(out) :: F_tstat
      integer :: F_istat
      !*/
      integer :: ii,jj,ni,nj,cnt
      real(RDOUBLE) :: norm
      real, dimension(size(F_data,dim=1),size(F_data,dim=2)) :: tfld
      real, dimension(:,:), pointer :: pfld=>null()
      !----------------------------------------------------------------------
      if (calc_nmembers < 3) then
         F_istat = RMN_ERR
         call msg(MSG_ERROR,'(calc_stat_mod) Numbers of members should be >= 3')
         return
      endif
      F_istat = RMN_OK
      ni = size(F_data,dim=1)
      nj = size(F_data,dim=2)
      norm = sqrt(dble(calc_nmembers-1))
      do jj=1,nj
         do ii=1,ni
            tfld(ii,jj) = norm * (F_eavg_8(ii,jj) - dble(F_data(ii,jj))) / sqrt(max(0.5*(F_evar_8(ii,jj)+F_evar0_8(ii,jj)),epsilon(F_evar_8)))
         enddo
      enddo
      pfld => tTest(tfld,2*(calc_nmembers-1))
      cnt = 0
      do jj=1,nj
         do ii=1,ni
            if (pfld(ii,jj) < F_crit) cnt = cnt+1
         enddo
      enddo
      deallocate(pfld)
      F_tstat = real(cnt)/real(ni*nj)
      !----------------------------------------------------------------------
   end function calc_nt

end module calc_stat_mod
